pkgname='nodejs'
pkgver='0.12.0'
pkgrel='1'
pkgdesc='Evented I/O for V8 javascript'
arch=('i686' 'amd64')
url='http://nodejs.org/'
license=('MIT')
depends=('openssl')
makedepends=('python')
checkdepends=('curl')
provides=('nodejs')
conflicts=('nodejs')
options=('!emptydirs')
branch=unstable
section=unknown
priority=extra
source=("http://nodejs.org/dist/v$pkgver/node-v$pkgver.tar.gz")
sha256sums=('9700e23af4e9b3643af48cef5f2ad20a1331ff531a12154eef2bfb0bb1682e32')

# Install missing deps and remove after build success
# It's equivalent for -s -r options passed to makedebpkg directly
DEP_BIN=1
RMDEPS=1

build() {
  cd "node-v$pkgver"

  ./configure \
    --prefix=/usr \
    --shared-openssl

  make -j5
}

check() {
  cd "node-v$pkgver"
  # Just for information. This tests is unstable :(
  make test || true
}

package() {
  cd "node-v$pkgver"

  make DESTDIR="$pkgdir" install

  # install docs as per user request
  install -d "$pkgdir"/usr/share/doc/nodejs
  cp -r doc/api/{*.html,assets} \
    "$pkgdir"/usr/share/doc/nodejs

  install -D -m644 LICENSE \
    "$pkgdir"/usr/share/licenses/nodejs/LICENSE
}
